// 2019-04-04
// + Create git: https://github.com/hybridgame/EECustom

BACKUP ~EECustom/backup~
AUTHOR ~HybridGame~
VERSION "2019.06.26"

LANGUAGE  ~All~
          ~All~
          ~EECustom/Language/setup.tra~

///////////////////////////////////////////////////////////////////////////////////////////////////
// BG1EE

// ------------------------------------------------------------------------------------------------
// Tutorial items and bag of holding in Candlekeep

BEGIN @101
REQUIRE_PREDICATE GAME_IS ~bgee~ @1
  
EXTEND_BOTTOM ~AR2600.BCS~ ~EECustom/script/bg1item.baf~
COPY ~EECustom/item/ttaxe1.itm~   ~override~
     ~EECustom/item/ttdagg1.itm~  ~override~

///////////////////////////////////////////////////////////////////////////////////////////////////
// BG2EE

// ------------------------------------------------------------------------------------------------
// Tutorial items and bag of holding in Irenicus dungeon

BEGIN @201
REQUIRE_PREDICATE GAME_IS ~bg2ee~ @2
  
EXTEND_BOTTOM ~AR0602.BCS~ ~EECustom/script/bg2item.baf~
COPY ~EECustom/item/ttaxe1.itm~   ~override~
     ~EECustom/item/ttdagg1.itm~  ~override~

// ------------------------------------------------------------------------------------------------
// Triple-class HLA tables

BEGIN @202
REQUIRE_PREDICATE GAME_IS ~bg2ee~ @2
  
COPY ~EECustom/2da/lufmc.2da~ ~override~
     ~EECustom/2da/lufmt.2da~ ~override~

///////////////////////////////////////////////////////////////////////////////////////////////////
// BGEE

// ------------------------------------------------------------------------------------------------
// Free action fix
BEGIN @306
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

ACTION_FOR_EACH file IN
    potn45.itm    // Potion of Freedom
    ring09.itm    // Ring of Free Action 
    sppr403.spl   // Free Action
    sw2h06.itm    // Spider's Bane
    sw2h13.itm    // Spider's Bane
    blun30.itm    // Flail of Ages +5
    npplat.itm    // Firecam Full-Plate Armor
    sper12.itm    // Ixil's Spike +6
    c2keld01.itm  // Firecam Full-Plate Armor
    c2mazz01.itm  // Sword of Arvoreen
    z_57.itm      // Valkyrie ring
BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ THEN
    BEGIN
        COPY_EXISTING ~%file%~  ~override~  // Potion of Freedom
            LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi613 END // Improved Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi305 END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spra301 END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spin572 END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 169 match_parameter2 = 110   END // Improved Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 169 match_parameter2 = 38    END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 110   END // Improved Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 38    END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14023 END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 16    END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 126   END // Movement Modifier
            LPF DELETE_EFFECT INT_VAR match_opcode = 126 match_parameter2 = 2     END // Movement Modifier
            BUT_ONLY_IF_IT_CHANGES
    END
END

// ------------------------------------------------------------------------------------------------
// Remove XP cap
BEGIN @301
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

COPY ~EECustom/2da/xpcap.2da~ ~override~
     ~EECustom/2da/xplevel.2da~ ~override~

ACTION_FOR_EACH file IN STARTARE.2da SODSTRTA.2da
BEGIN
    COPY_EXISTING ~%file%~ ~override~
        REPLACE_TEXTUALLY ~\(START_\(MP_\)?XP_CAP[ %TAB%]+\)[0-9]+~ ~\1-1~
        BUT_ONLY IF_EXISTS
END

// ------------------------------------------------------------------------------------------------
// Un-nerfed bard spell progression table

BEGIN @302
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

COPY ~EECustom/2da/mxsplbrd.2da~ ~override~

// ------------------------------------------------------------------------------------------------
// Easy spell learning

BEGIN @303
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

COPY_EXISTING ~intmod.2da~ ~override~
    COUNT_2DA_ROWS ~3~ "rows"
    FOR (index = 3; index < rows ; index = index + 1)
    BEGIN
        SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
    END
    SET_2DA_ENTRIES_NOW ~intmod~ 1
    BUT_ONLY

// ------------------------------------------------------------------------------------------------
// Happy patch

BEGIN @304
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
    REPLACE_TEXTUALLY ~-[0-9]+~ ~0~
    PRETTY_PRINT_2DA
    BUT_ONLY

// bg1 npc can let shar-teel leave anyway
COPY_EXISTING ~sharteel.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~[ %TAB%]Global("X#ShHighReputation","GLOBAL",[01])~ ~ False()~
        REPLACE_TEXTUALLY ~[ %TAB%]Global("X#ShHighMale","GLOBAL",[01])~ ~ False()~
    END
    BUT_ONLY IF_EXISTS

// ------------------------------------------------------------------------------------------------
// Unlimited stacking
BEGIN @305
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

OUTER_SET stack = 999
INCLUDE ~EECustom/lib/increase_ammo.tpa~
INCLUDE ~EECustom/lib/increase_gem.tpa~
INCLUDE ~EECustom/lib/increase_potion.tpa~