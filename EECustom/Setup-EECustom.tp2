BACKUP ~EECustom/backup~
AUTHOR ~HybridGame~
VERSION "2022-11-06"

LANGUAGE  ~All~ ~All~ ~EECustom/Language/setup.tra~

///////////////////////////////////////////////////////////////////////////////////////////////////
// BGEE

// ------------------------------------------------------------------------------------------------
// SotSC bug patch

BEGIN @100
REQUIRE_PREDICATE FILE_EXISTS ~override/L#ORM1.are~ @101
REQUIRE_PREDICATE FILE_EXISTS ~override/L#ORM2.are~ @101

COPY_EXISTING L#ORM1.are override
    LPF fj_are_structure
        INT_VAR
        fj_type = 2 // travel
        fj_box_left = 2200
        fj_box_top = 1
        fj_box_right = 2400
        fj_box_bottom = 30
        fj_cursor_idx = 34
        fj_flags = 1540 // no_NPCs = 512, party_required = save = 4
        fj_alt_x = 2300
        fj_alt_y = 15
        fj_loc_x = 2300
        fj_loc_y = 15
        fj_vertex_0 = 2200 + (1 << 16)
        fj_vertex_1 = 2400 + (1 << 16)
        fj_vertex_2 = 2400 + (30 << 16)
        fj_vertex_3 = 2200 + (30 << 16)
        STR_VAR
        fj_structure_type = region
        fj_name = TRAR5100
        fj_destination_area = ar5100
        fj_destination_name = L#ORM
    END
    LPF fj_are_structure
        INT_VAR
        fj_type = 2 // travel
        fj_box_left = 1000
        fj_box_top = 2830
        fj_box_right = 1800
        fj_box_bottom = 2870
        fj_cursor_idx = 34
        fj_flags = 1540 // no_NPCs = 512, party_required = save = 4
        fj_alt_x = 1400
        fj_alt_y = 2850
        fj_loc_x = 1400
        fj_loc_y = 2850
        fj_vertex_0 = 1000 + (2830 << 16)
        fj_vertex_1 = 1800 + (2830 << 16)
        fj_vertex_2 = 1800 + (2870 << 16)
        fj_vertex_3 = 1000 + (2870 << 16)
        STR_VAR
        fj_structure_type = region
        fj_name = TRL#ORM2
        fj_destination_area = L#ORM2
        fj_destination_name = En
    END
    
COPY_EXISTING L#ORM2.are override
    LPF fj_are_structure
        INT_VAR
        fj_type = 2 // travel
        fj_box_left = 800
        fj_box_top = 1
        fj_box_right = 1600
        fj_box_bottom = 40
        fj_cursor_idx = 34
        fj_flags = 1540 // no_NPCs = 512, party_required = save = 4
        fj_alt_x = 1200
        fj_alt_y = 20
        fj_loc_x = 1200
        fj_loc_y = 20
        fj_vertex_0 = 800 + (1 << 16)
        fj_vertex_1 = 1600 + (1 << 16)
        fj_vertex_2 = 1600 + (40 << 16)
        fj_vertex_3 = 800 + (40 << 16)
        STR_VAR
        fj_structure_type = region
        fj_name = TRL#ORM1
        fj_destination_area = L#ORM1
        fj_destination_name = En3
    END

///////////////////////////////////////////////////////////////////////////////////////////////////
// BG2EE

// ------------------------------------------------------------------------------------------------
// Triple-class HLA tables

BEGIN @202
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @2
  
COPY ~EECustom/2da/lufmc.2da~ ~override~
     ~EECustom/2da/lufmt.2da~ ~override~

///////////////////////////////////////////////////////////////////////////////////////////////////
// BGEE & BG2EE

// ------------------------------------------------------------------------------------------------
// Tutorial items and containters
BEGIN @300
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @3

ACTION_IF GAME_IS ~bgee eet~
BEGIN
    EXTEND_BOTTOM ~AR2600.BCS~ ~EECustom/script/bg1item.baf~
    ACTION_FOR_EACH store IN 31 02 03 05 06
    BEGIN
        COPY_EXISTING ~bag%store%.sto~ ~override~
            WRITE_SHORT 0x22 30000
    END
    EXTEND_TOP ~SHOAL.BCS~ ~EECustom/script/shoal.baf~
END
ELSE BEGIN
    EXTEND_BOTTOM ~AR0602.BCS~ ~EECustom/script/bg2item.baf~
    ACTION_FOR_EACH store IN 02 03 04 05 06
    BEGIN
        COPY_EXISTING ~bag%store%.sto~ ~override~
            WRITE_SHORT 0x22 30000
    END
END


// ------------------------------------------------------------------------------------------------
// Free action fix
BEGIN @306
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @3

ACTION_FOR_EACH file IN
    potn45.itm    // Potion of Freedom
    ring09.itm    // Ring of Free Action 
    bdring09.itm  // Ring of Free Action 
    freering.itm  // Ring of Free Action 
    sppr403.spl   // Free Action
    sw2h06.itm    // Spider's Bane
    sw2h13.itm    // Spider's Bane
    blun30.itm    // Flail of Ages +5
    npplat.itm    // Firecam Full-Plate Armor
    sper12.itm    // Ixil's Spike +6
    c2keld01.itm  // Firecam Full-Plate Armor
    c2mazz01.itm  // Sword of Arvoreen
    z_33.itm      // Windforce
    z_57.itm      // Valkyrie ring
    thring01.itm  // Ring of Free Action
    thring02.itm  // Ring of Free Action
    s#ring01      // Ring of Free Action +1
    s#ring02      // Ring of Free Action +2
    u#hfssls      // Lolth's Cruel Sting
BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ THEN
    BEGIN
        COPY_EXISTING ~%file%~  ~override~
            PATCH_FOR_EACH spell IN spwi613 spwi305 spra301 spin572
            BEGIN   // Protection from Spell
                LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = EVAL ~%spell%~ END
            END
            PATCH_FOR_EACH icon IN 38 110
            BEGIN   // Prevent Special Effect Icon, Remove Special Effect Icon
                LPF DELETE_EFFECT INT_VAR match_opcode = 169 match_parameter2 = icon END
                LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = icon END
            END
            // Protection from Display Specific String (Hasted)
            LPF DELETE_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14023 END
            // Protection from Opcode
            LPF DELETE_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 16    END // Haste
            LPF DELETE_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 126   END // Movement Modifier
            // Movement Modifier
            LPF DELETE_EFFECT INT_VAR match_opcode = 126 match_parameter2 = 1     END // Movement Modifier
            LPF DELETE_EFFECT INT_VAR match_opcode = 126 match_parameter2 = 2     END // Movement Modifier
            BUT_ONLY_IF_IT_CHANGES
    END
END

// ------------------------------------------------------------------------------------------------
// Remove XP cap
BEGIN @301
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @3

COPY ~EECustom/2da/xpcap.2da~ ~override~
     ~EECustom/2da/xplevel.2da~ ~override~
     ~EECustom/2da/WSPATCK.2da~ ~override~

ACTION_FOR_EACH file IN STARTARE.2da SODSTRTA.2da
BEGIN
    COPY_EXISTING ~%file%~ ~override~
        REPLACE_TEXTUALLY ~\(START_\(MP_\)?XP_CAP[ %TAB%]+\)[0-9]+~ ~\1-1~
        BUT_ONLY IF_EXISTS
END

// ------------------------------------------------------------------------------------------------
// Un-nerfed bard spell progression table

BEGIN @302
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @3

COPY ~EECustom/2da/mxsplbrd.2da~ ~override~

// ------------------------------------------------------------------------------------------------
// Easy spell learning

BEGIN @303
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @3

COPY_EXISTING ~intmod.2da~ ~override~
    COUNT_2DA_ROWS ~3~ "rows"
    FOR (index = 3; index < rows ; ++index)
    BEGIN
        SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
    END
    SET_2DA_ENTRIES_NOW ~intmod~ 1
    BUT_ONLY

// ------------------------------------------------------------------------------------------------
// Happy patch

BEGIN @304
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @3

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
    REPLACE_TEXTUALLY ~-[0-9]+~ ~0~
    PRETTY_PRINT_2DA
    BUT_ONLY

// bg1 npc can let shar-teel leave anyway
COPY_EXISTING ~sharteel.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~[ %TAB%]Global("X#ShHighReputation","GLOBAL",[01])~ ~ False()~
        REPLACE_TEXTUALLY ~[ %TAB%]Global("X#ShHighMale","GLOBAL",[01])~ ~ False()~
    END
    BUT_ONLY IF_EXISTS

// ------------------------------------------------------------------------------------------------
// Unlimited stacking
BEGIN @305
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @3

OUTER_SET stack = 999
INCLUDE ~EECustom/lib/increase_ammo.tpa~
INCLUDE ~EECustom/lib/increase_potion.tpa~
INCLUDE ~EECustom/lib/increase_gem.tpa~