// 2019-04-04
// + Create git: https://github.com/hybridgame/EECustom

BACKUP ~EECustom/backup~
AUTHOR ~Investor~
LANGUAGE ~All~  ~All~ ~EECustom/Language/setup.tra~

//////////////////////////////////////////////////////////////////////////
// BG1
//////////////////////////////////////////////////////////////////////////

// Tutorial items and bag of holding in Candlekeep

BEGIN @101
REQUIRE_PREDICATE GAME_IS ~bgee~ @1
  
EXTEND_BOTTOM ~AR2600.BCS~ ~EECustom/script/bg1item.baf~
COPY ~EECustom/item/ttaxe1.itm~ ~override~
COPY ~EECustom/item/ttdagg1.itm~ ~override~

//////////////////////////////////////////////////////////////////////////
// BG2
//////////////////////////////////////////////////////////////////////////

// -----------------------------------------------------------------------
// Tutorial items and bag of holding in BG2 Irenicus dungeon

BEGIN @201
REQUIRE_PREDICATE GAME_IS ~bg2ee~ @2
  
EXTEND_BOTTOM ~AR0602.BCS~ ~EECustom/script/bg2item.baf~
COPY ~EECustom/item/ttaxe1.itm~ ~override~
COPY ~EECustom/item/ttdagg1.itm~ ~override~

// -----------------------------------------------------------------------
// Triple-class HLA tables

BEGIN @202
REQUIRE_PREDICATE GAME_IS ~bg2ee~ @2
  
COPY ~EECustom/2da/lufmc.2da~ ~override~
COPY ~EECustom/2da/lufmt.2da~ ~override~

//////////////////////////////////////////////////////////////////////////
// BG1 + BG2
//////////////////////////////////////////////////////////////////////////

// -----------------------------------------------------------------------
// Remove XP cap

BEGIN @301
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

COPY ~EECustom/2da/xpcap.2da~ ~override~
COPY ~EECustom/2da/xplevel.2da~ ~override~

ACTION_IF FILE_EXISTS_IN_GAME ~STARTARE.2da~ THEN BEGIN
  COPY_EXISTING ~STARTARE.2da~ ~override~
    REPLACE_TEXTUALLY ~\(START_\(MP_\)?XP_CAP[ %TAB%]+\)[0-9]+~ ~\1-1~
    BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~SODSTRTA.2da~ THEN BEGIN
  COPY_EXISTING ~SODSTRTA.2da~ ~override~
    REPLACE_TEXTUALLY ~\(START_\(MP_\)?XP_CAP[ %TAB%]+\)[0-9]+~ ~\1-1~
    BUT_ONLY
END

// -----------------------------------------------------------------------
// Un-nerfed bard spell progression table

BEGIN @302
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

COPY ~EECustom/2da/mxsplbrd.2da~ ~override~

// -----------------------------------------------------------------------
// Easy spell learning

BEGIN @303
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

COPY_EXISTING ~intmod.2da~ ~override~
  COUNT_2DA_ROWS ~3~ "rows"
  FOR (index = 3; index < rows ; index = index + 1) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  BUT_ONLY

// -----------------------------------------------------------------------
// Happy patch

BEGIN @304
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  REPLACE_TEXTUALLY ~-[0-9]+~ ~0~
  PRETTY_PRINT_2DA
  BUT_ONLY

// bg1 npc can let shar-teel leave anyway
COPY_EXISTING ~sharteel.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~[ %TAB%]Global("X#ShHighReputation","GLOBAL",[01])~ ~ False()~
    REPLACE_TEXTUALLY ~[ %TAB%]Global("X#ShHighMale","GLOBAL",[01])~ ~ False()~
  END
  BUT_ONLY IF_EXISTS

// -----------------------------------------------------------------------
// Unlimited Stacking
BEGIN @305
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @3

OUTER_SET stack = 999
INCLUDE ~EECustom/lib/increase_ammo.tpa~
OUTER_SET stack = 999
INCLUDE ~EECustom/lib/increase_gem.tpa~
OUTER_SET stack = 999
INCLUDE ~EECustom/lib/increase_potion.tpa~
OUTER_SET stack = 999
INCLUDE ~EECustom/lib/increase_scroll.tpa~